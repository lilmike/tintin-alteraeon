#ACTION {^A magical hammer disappears in a blinding flash of light! (worn by you)}
{
	#if {"${skillsAndSpells.spirit hammer}" == "true" && "${auto.recast}" == "true"}
	{
		c spirit
	}
}
{9}

#ACTION {^casting_%1_You cast a powerful healing spell on yourself.$}
{
	FULL_HEAL
}
{9}

#ACTION {^casting_%1_You try to heal some of your {minor|more critical|more serious} {injuries|wounds}.$}
{
	FULL_HEAL
}
{9}

#ALIAS {H}
{
	@harm{%0}
}
{9}

#ALIAS {^/$}
{
	#if {${character.currentMana} >= 17 && "${skillsAndSpells.word of recall}" == "true"}
	{
		c word of recall
	};
	#else
	{
		recall
	}
}
{9}

#ALIAS {^FULL_HEAL$}
{
	#if {"&{character.fullHeal}"  && ${character.currentMana} > ${character.maxMana} / 5 && ${character.currentHp} < ${character.maxHp} && "${character.inBattle}" != "true" && "$busy" != "true"}
	{
		#delay {0.6} {c ${character.fullHeal}};
		#variable {msg} {false}
	};
	#elseif {${character.currentMana} < ${character.maxMana} / 5 && &{character.fullHeal}}
	{
		#unvariable {character.fullHeal};
		#variable {msg} {Fullheal interupted due to low mana.}
	};
	#elseif {${character.currentHp} >= ${character.maxHp}  && &{character.fullHeal}}
	{
		#unvariable {character.fullHeal};
		#variable {msg} {Fullheal complete.}
	};
	#elseif {"${character.inBattle}" == "true" || "$busy" == "true" && &{character.fullHeal}}
	{
		#unvariable {character.fullHeal}
	};
	#if {&{msg} && "$msg" != "false"}
	{
		#echo {$msg};
		#variable {msg} {false}
	}
}
{9}

#ALIAS {^autorecall$}
{
	#variable {autorecall} {true};
	#showme {You will automatically recall if you are in awful or pretty hurt.}
}
{9}

#ALIAS {^fullheal$}
{
	#if {"${skillsAndSpells.major heal}" == "true"}
	{
		#variable {character.fullHeal} {major heal}
	};
	#elseif {"${skillsAndSpells.heal}" == "true"}
	{
		#variable {character.fullHeal} {heal}
	};
	#elseif {"${skillsAndSpells.bolster}" == "true"}
	{
		#variable {character.fullHeal} {bolster}
	};
	#else
	{
		#variable {character.fullHeal} {soothe wounds}
	};
	cast ${character.fullHeal}
}
{9}

#ALIAS {^noautorecall$}
{
	#unvariable {autorecall};
	#showme {You will no automatically recall.}
}
{9}

#ALIAS {h}
{
	@heal{%0}
}
{9}

#FUNCTION {harm}
{
	#if {"${skillsAndSpells.harm}" == "true"}
	{
		#variable {character.harmSpell} {harm}
	};
	#elseif {"${skillsAndSpells.inflict suffering}" == "true"}
	{
		#variable {character.harmSpell} {inflict suffering}
	};
	#elseif {"${skillsAndSpells.chasten}" == "true"}
	{
		#variable {character.harmSpell} {chasten}
	};
	#else
	{
		#variable {character.harmSpell} {inflict wounds}
	};
	#variable {result} {c ${character.harmSpell} %0}
}

#FUNCTION {heal}
{
	#if {"%0" == "" && ${character.currentHp} == ${character.maxHp}}
	{
		#variable {result} {#echo {Your health is already at maximum.}}
	};
	#else
	{
		#if {"${skillsAndSpells.breath of life}" == "true"}
		{
			#variable {character.healSpell} {breath of life}
		};
		#elseif {"${skillsAndSpells.major heal}" == "true"}
		{
			#variable {character.healSpell} {major heal}
		};
		#elseif {"${skillsAndSpells.heal}" == "true"}
		{
			#variable {character.healSpell} {heal}
		};
		#elseif {"${skillsAndSpells.bolster}" == "true"}
		{
			#variable {character.healSpell} {bolster}
		};
		#else
		{
			#variable {character.healSpell} {soothe wounds}
		};
		#variable {result} {c ${character.healSpell} %0}
	}
}
