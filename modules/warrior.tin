#ACTION {^You are rescued from certain death by %1!}
{
	a
}
{1}

#ACTION {^You fail the rescue!}
{
	rescue $lastRescued
}
{1}

#ACTION {^You find yourself too weak to use %1.$}
{
	#variable {rewield} {true};
	eq | grep wielded | prefix silent_
}
{5}

#ACTION {^silent_wielded     - $}
{
	#unvariable {weapon}
}
{5}

#ACTION {^But no-one is attacking {her|him|it}!$}
{
	#math {rescueIncrementer} {$rescueIncrementer + 1};
	#replace {lastRescued} {%d} {$rescueIncrementer};
	rescue $lastRescued
}
{9}

#ACTION {^The physical reserve deep within you feels replenished.$}
{
	#unvariable {character.exhausted};
	play reserve-replenish
}
{9}

#ACTION {^You discover a way to use %1 in combination!$}
{
	Add_Event %0;
	play new-combo
}
{9}

#ACTION {^You do not see anyone named that here.$}
{
	#variable {rescueIncrementer} {1}
}
{9}

#ACTION {^You don't have enough energy to do that again so soon.$}
{
	#send {assist}
}
{9}

#ACTION {^You got a %d attack combo%2}
{
	play combo-partial-%1
}
{9}

#ACTION {^You heroically rescue %*}
{
	#variable {rescueIncrementer} {1}
}
{9}

#ACTION {^You successfully finished a full %d attack combo!}
{
	play combo-full-%1
}
{9}

#ACTION {^You {feel your enhanced courage fading|make a cut and paint yourself with your own blood, but don't feel any different}.$}
{
	#if {${character.currentHp} > ${character.maxHp} / 2}
	{
		@command{val;valor}
	};
	#if {"%1" == "feel your enhanced courage fading"}
	{
		play valor-drop
	}
}
{9}

#ACTION {^silent_ Length %d \x7c %2}
{
	#variable {combo.attack} {%2};
	#replace {combo.attack} {\x7c} { ${combo.target};};
	#replace {combo.attack} {berserking} {berserk};
	#replace {combo.attack} {leap attack} {leap};
	#replace {combo.attack} {pommel strike} {pommel};
	#replace {combo.attack} {x} {randomattack};
	#if {"${combo.setKey}" != "false"}
	{
		set${combo.setKey} ${combo.attack}
	};
	#else
	{
		${combo.attack}
	};
	#variable {combo.target} {};
	#variable {combo.setKey} {false}
}
{9}

#ACTION {^silent_combo_wc - lines:  %d}
{
	#math {i} {1d%1};
	tactics all | grep -i length | grep -v x | grep -v charge | grep -v taunt | prefix silent_ | tail -n $i | head -n 1
}
{9}

#ALIAS {^randomattack%s%2}
{
	get_random_attack;
	#list {randomAttack} {size} {i};
	#math {r} {1d$i};
	#if {$i >= 3}
	{
		#while {$r == ${lastRandomAttack[1]} || $r == ${lastRandomAttack[2]}}
		{
			#math {r} {1d$i}
		}
	};
	#variable {lastRandomAttack[2]} {${lastRandomAttack[1]}};
	#variable {lastRandomAttack[1]} {$r};
	${randomAttack[$r]} %2
}
{5}

#ALIAS {{wi|wie|wiel|wield} %1}
{
	#if {"%1" != "$stabWeapon"}
	{
		#variable {weapon} {%1}
	};
	\wield %1
}
{5}

#ALIAS {^re{s|sc|scu|scue} %s%d.%4}
{
	#variable {rescueIncrementer} {%3};
	#variable {lastRescued} {%4};
	#send {%0}
}
{8}

#ALIAS {^xcombo %S %S$}
{
	#variable {combo.target} {%2 };
	tactics all | grep %1 | grep x | prefix silent_ | head -n 1
}
{8}

#ALIAS {^{3|4|5|6|7|8}{combo|acombo|ccombo|lcombo} %3$}
{
	#variable {combo.target} {};
	#switch {"%2"}
	{
		#case {"acombo"}
		{
			tactics advanced | grep %1 | grep %3 | grep -v x | prefix silent_ | head -n 1
		};
		#case {"ccombo"}
		{
			tactics cross class | grep %1 | grep %3 | grep -v x | prefix silent_ | head -n 1
		};
		#case {"lcombo"}
		{
			tactics low | grep %1 | grep %3 | grep -v x | prefix silent_ | head -n 1
		};
		#default
		{
			tactics all | grep %1 | grep %3 | grep -v x | prefix silent_ | head -n 1
		}
	}
}
{8}

#ALIAS {^{combo|acombo|ccombo|lcombo} %S %S$}
{
	#variable {combo.target} {%3 };
	#switch {"%1"}
	{
		#case {"acombo"}
		{
			tactics advanced | grep %2 | grep -v x | prefix silent_ | head -n 1
		};
		#case {"ccombo"}
		{
			tactics cross class | grep %2 | grep -v x | prefix silent_ | head -n 1
		};
		#case {"lcombo"}
		{
			tactics low | grep %2 | grep -v x | prefix silent_ | head -n 1
		};
		#default
		{
			tactics all | grep %2 | grep -v x | prefix silent_ | head -n 1
		}
	}
}
{8}

#ALIAS {^combo$}
{
	#variable {combo.target} {};
	tactics all | grep -i length | grep -v x | grep -v charge | grep -v taunt | wc -l | prefix silent_combo_
}
{9}

#ALIAS {^get_random_attack$}
{
	#list {randomAttack} {clear};
	#if {"${skillsAndSpells.bash}" == "true"}
	{
		#list {randomAttack} {add} {bash}
	};
	#if {"${skillsAndSpells.bloodletting stab}" == "true" && "$crossClassTactics" == "true"}
	{
		#list {randomAttack} {add} {stab}
	};
	#if {"${skillsAndSpells.charge}" == "true" && "$inBattle" != "true"}
	{
		#list {randomAttack} {add} {charge}
	};
	#if {"${skillsAndSpells.cleave}" == "true" && "$advancedTactics" == "true"}
	{
		#list {randomAttack} {add} {cleave}
	};
	#if {"${skillsAndSpells.disarm}" == "true" && "$advancedTactics" == "true"}
	{
		#list {randomAttack} {add} {disarm}
	};
	#if {"${skillsAndSpells.feint}" == "true" && "$inBattle" == "true"}
	{
		#list {randomAttack} {add} {feint}
	};
	#if {"${skillsAndSpells.ground strike}" == "true" && "$inBattle" == "true" && "$advancedTactics" == "true"}
	{
		#list {randomAttack} {add} {gstrike}
	};
	#if {"${skillsAndSpells.flourish}" == "true" && "$inBattle" == "true"}
	{
		#list {randomAttack} {add} {flourish}
	};
	#if {"${skillsAndSpells.intimidate}" == "true" && "$advancedTactics" == "true"}
	{
		#list {randomAttack} {add} {intimidate}
	};
	#if {"${skillsAndSpells.jab}" == "true"}
	{
		#list {randomAttack} {add} {jab}
	};
	#if {"${skillsAndSpells.kick}" == "true"}
	{
		#list {randomAttack} {add} {kick}
	};
	#if {"${skillsAndSpells.knockback}" == "true"}
	{
		#list {randomAttack} {add} {knockback}
	};
	#if {"${skillsAndSpells.leap attack}" == "true"}
	{
		#list {randomAttack} {add} {leap}
	};
	#if {"${skillsAndSpells.lunge}" == "true"}
	{
		#list {randomAttack} {add} {lunge}
	};
	#if {"${skillsAndSpells.pommel strike}" == "true"}
	{
		#list {randomAttack} {add} {pommel}
	};
	#if {"${skillsAndSpells.riposte}" == "true" && "$inBattle" == "true"}
	{
		#list {randomAttack} {add} {riposte}
	};
	#if {"${skillsAndSpells.stomp}" == "true"}
	{
		#list {randomAttack} {add} {stomp}
	};
	#if {"${skillsAndSpells.taunt}" == "true" && "$inBattle" != "true"}
	{
		#list {randomAttack} {add} {taunt}
	};
	#if {"${skillsAndSpells.thrust}" == "true"}
	{
		#list {randomAttack} {add} {thrust}
	};
	#if {"${skillsAndSpells.trip}" == "true" && "$crossClassTactics" == "true"}
	{
		#list {randomAttack} {add} {trip}
	};
	#if {"${skillsAndSpells.whirlwind}" == "true" && "$advancedTactics" == "true"}
	{
		#list {randomAttack} {add} {whirlwind}
	};
	#list {randomAttack} {size} {i};
	#if {$i == 0}
	{
		#list {randomAttack} {add} {#echo {You do not currently have any warrior attacks. If you believe this is wrong, try the load command.}};
		#unvariable {lastRandomAttack}
	}
}
{9}

#ALIAS {^randomattack%s}
{
	get_random_attack;
	#list {randomAttack} {size} {i};
	#math {r} {1d$i};
	if {$i >= 3}{#while {$r == ${lastRandomAttack[1]} || $r == ${lastRandomAttack[2]}}{#math {r} {1d$i}}};
	#variable {lastRandomAttack[2]} {${lastRandomAttack[1]}};
	#variable {lastRandomAttack[1]} {$r};
	${randomAttack[$r]}
}
{9}

#ALIAS {^re{s|sc|scu|scue} %s%3}
{
	#variable {lastRescued} {1.%3};
	#send {%0}
}
{9}

#ALIAS {^xcombo %S$}
{
	#variable {combo.target} {};
	tactics all | grep %1 | grep x | prefix silent_ | head -n 1
}
{9}

#ALIAS {^xcombo$}
{
	#variable {combo.target} {};
	tactics all | grep -i length | grep x | grep -v charge | grep -v taunt | wc -l | prefix silent_combo_
}
{9}

#ALIAS {^{3|4|5|6|7|8}{combo|lcombo|acombo|ccombo}set{a|b|c|d|e|f|g|h|n|q|r|s|t|v|w|x|y|z} %4}
{
	#variable {combo.setKey} {%3};
	%1%2 %4
}
{9}

#ALIAS {^{auto|noauto}{charge|retreat|valor}$}
{
	#if {"%1" == "auto"}
	{
		#variable {auto.%2} {true};
		#echo {auto %2 enabled.}
	};
	#else
	{
		#variable {auto.%2} {false};
		#showme {auto %2 disabled.}
	}
}
{9}

#ALIAS {^{a|as|ass|assi|assis|assist}$}
{
	#if {"${skillsAndSpells.charge}" == "true" && "${auto.charge}" == "true"}
	{
		charge
	};
	#else
	{
		#send {assist}
	}
}
{9}

#ALIAS {^{combo|acombo|ccombo|lcombo} %S$}
{
	#variable {combo.target} {};
	#switch {"%1"}
	{
		#case {"acombo"}
		{
			tactics advanced | grep %2 | grep -v x | prefix silent_ | head -n 1
		};
		#case {"ccombo"}
		{
			tactics cross class | grep %2 | grep -v x | prefix silent_ | head -n 1
		};
		#case {"lcombo"}
		{
			tactics low | grep %2 | grep -v x | prefix silent_ | head -n 1
		};
		#default
		{
			tactics all | grep %2 | grep -v x | prefix silent_ | head -n 1
		}
	}
}
{9}

#ALIAS {^{combo|lcombo|acombo|ccombo}set{a|b|c|d|e|f|g|h|n|q|r|s|t|v|w|x|y|z} %3}
{
	#variable {combo.setKey} {%2};
	%1 %3
}
{9}

#VARIABLE         {combo}  {}
#VARIABLE         {exhausted}  {}
#VARIABLE         {lastRandomAttack}  {{1}{0}{2}{0}}
#VARIABLE         {lastRescued}  {}
#VARIABLE         {rescueIncrementer}  {1}
