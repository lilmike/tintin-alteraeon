#ACTION {%1 {\(.*\(unique\)|\(unique\)}$}
{
	play unique;
	#if {"${auto[getunique]}" == "true"}
	{
		#regexp {%1} {{.*} {.*}} {get &2 corpse;#if {"${auto[donateunique]}" == "true"} {donate &2}};
		#else
		{
			get %1 corpse
		}
	}
}
{9}

#ACTION {^junk_You drop %*.$}
{
	sac $junkItem #unvariable {junkItem}
}
{9}

#ACTION {^junk_You insult %1 %S.$}
{
	drop %2 | prefix junk_;
	#variable {junkItem} {%2}
}
{9}

#ACTION {^scrollcounter_Found %d items matching the criteria: %*}
{
	#variable {channelSilence} {true};
	#variable {msg} {%1 scrolls remaining.};
	Channel_Event all $msg;
	#showme {$msg};
	#if {%1 <= 20}
	{
		synth_play warningSound
	}
}
{9}

#ACTION {^silent_%*$battleScroll}
{
	get $battleScroll corpse;
	put $battleScroll ${character[container]}
}
{9}

#ACTION {^silent_You insult {corpse carcass body|carcass gutted corpse} %S.$}
{
	#variable {corpseVnum} {%2};
	#send {get coins $corpseVnum | grep -v There's nothing left};
	#if {"${skillsAndSpells[corpsecutter]}" == "true" && !&{inBattle}}
	{
		harvest components $corpseVnum | grep -v Looks like someone else beat you to it
	};
	#if {"${skillsAndSpells[harvest bone]}" == "true" && !&{inBattle}}
	{
		harvest bone $corpseVnum | grep -v has no useful bones remaining
	};
	#if {"${skillsAndSpells[salvage equipment]}" == "true" || &{battleScroll}}
	{
		search $corpseVnum | grep -v unique | prefix silent_ | grep -v There's nothing left
	};
	#if {"${skillsAndSpells[consume]}" == "true"}
	{
		control all consume $corpseVnum | grep -v There's nothing left
	};
	#if {"${skillsAndSpells[blood sacrifice]}" == "true"}
	{
		bsac $corpseVnum | grep -v no longer suitable
	};
	#if {"$busy" != "true"}
	{
		sac $corpseVnum
	};
	#unvariable {corpseVnum}
}
{9}

#ALIAS {^autostore %S}
{
	#variable {character[soulstoneContainer]} {%1};
	#variable {auto[store]} {true};
	#showme {You will now put soulstones in your ${character[soulstoneContainer]}\.}
}
{8}

#ALIAS {^autostore$}
{
	#if {!&{character[soulstoneContainer]}}
	{
		#variable {character[soulstoneContainer]} {${character[container]}}
	};
	#variable {auto[store]} {true};
	#showme {You will now put soulstones in your ${character[soulstoneContainer]}\.}
}
{9}

#ALIAS {^cont{a|ai|ain|aine|ainer} %S$}
{
	#showme {Container set to %2.};
	#variable {character[container]} {%2}
}
{9}

#ALIAS {^x$}
{
	insult corpse | prefix silent_
}
{9}

#ALIAS {^{auto|noauto}{donate|get}unique$}
{
	#switch {"%0"}
	{
		#case {"autogetunique"}
		{
			#variable {auto[getunique]} {true};
			#variable {auto[donateunique]} {false};
			#showme {You will automatically loot unique items from corpses.}
		};
		#case {"autodonateunique"}
		{
			#variable {auto[donateunique]} {true};
			#variable {auto[getunique]} {true};
			#showme {You will automatically loot unique items from corpses and then donate them.}
		};
		#default
		{
			#variable {auto[getunique]} {false};
			#variable {auto[donateunique]} {false};
			#showme {You will no longer automatically loot unique items from corpses.}
		}
	}
}
{9}

#ALIAS {^{g|ge|get|gi|giv|give|p|pu|put} %2 %S$}
{
	#if {!&{character[container]} && "%1" == "{p|pu|put}"}
	{
		#variable {character[container]} {%3}
	};
	#variable {i} {%2};
	#replace {i} {,} {;};
	#local {j} {};
	#foreach {$i} {j}
	{
		#send {%1 $j %3}
	}
}
{9}

#ALIAS {^{noscro|noscrol|noscroll}$}
{
	#unvariable {battleScroll};
	#showme {Battle scroll cleared.}
}
{9}

#ALIAS {^{scro|scrol|scroll} %1}
{
	#variable {battleScroll} {%1};
	#showme {You can now use alt+backslash to use get the %1 from your container and use it.}
}
{9}

#ALIAS {^{scro|scrol|scroll}$}
{
	#if {!&{battleScroll}}
	{
		#showme {You don't have a battle scroll set. To set it type scroll and the item you want to use, E.g. scroll potion.}
	};
	#else
	{
		#regexp {$battleScroll} {{.*} {.*}} {l in ${character[container]} &2 | prefix scrollcounter_};
		#else
		{
			l in ${character[container]} $battleScroll | prefix scrollcounter_
		}
	}
}
{9}

#ALIAS {g}
{
	#send {get all corpse}
}
{9}

#ALIAS {junk %1}
{
	insult ?%1 -inventory | prefix junk_
}
{9}

#ALIAS {lic}
{
	look in corpse
}
{9}

#ALIAS {noautostore}
{
	#unvariable {auto[store]};
	#showme {You will no longer autostore anything.}
}
{9}

#ALIAS {rm}
{
	remove
}
{9}

#GAG              {^You don't see anything named 'coins' in the corpse of %1.$}
#GAG              {^junk_You insult%*}
#GAG              {^scrollcounter_%*}
#MACRO {\e\}
{
	#regexp {$battleScroll} {{.*} {.*}} {get $battleScroll ${character[container]};use &2;l in ${character[container]} +ncount &2 +count | prefix scrollcounter_};
	#else
	{
		get $battleScroll ${character[container]};
		use $battleScroll;
		l in ${character[container]} +ncount $battleScroll +count | prefix scrollcounter_
	}
}

