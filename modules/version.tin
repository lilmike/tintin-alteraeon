#ACTION {^%w tells you, '!ping'$}
{
	#if {{${client[lastPingName]}} != {%1}}
	{
		#line {gag};
		tell %1 tintin-alteraeon V${client[tintinAlterAeonVersionNumber]} by Lilmike and contributors: https://github.com/lilmike/tintin-alteraeon (This is the ${client[release]} version);
		#variable {client[lastPingName]} {%1}
	};
	#else
	{
		synth_play tellReceiveSound
	}
}
{5}

#ACTION {^silent_Version Tag gtt-{beta|stable} %s- %S %sOwners: %5}
{
	#if {{${client[tintinAlterAeonVersionNumber]}} != {%3}}
	{
		synth_play notificationSound;
		Channel_Event all Your version of tintin-alteraeon is out of date. Please update as soon as possible to get the latest features and bug fixes.;
		lecho Your version of tintin-alteraeon is out of date. Please update as soon as possible to get the latest features and bug fixes.
	}
}
{9}

#ALIAS {^GET_UUID$}
{
	#script {client[tintinAlterAeonUUID]} {if ! [ -f "${HOME}/UUID" ] ; then echo "$(($(date +%s%N)/1000000))" | md5sum | cut -c -10 > "${HOME}/UUID";fi;cat "${HOME}/UUID"};
	#variable {client[tintinAlterAeonUUID]} {${client[tintinAlterAeonUUID][1]}}
}
{9}

#ALIAS {^SHOW_VERSION$}
{
	GET_UUID;
	lecho _gtt++ ${client[tintinAlterAeonVersionNumber]} ${client[tintinAlterAeonUUID]};
	lecho tintin-alteraeon V${client[tintinAlterAeonVersionNumber]} by Lilmike and contributors: https://github.com/lilmike/tintin-alteraeon;
	#if {{${client[release]}} == {beta}}
	{
		lecho Thanks for testing the beta code. If you find bugs please tell us about them on the cowsay channel.
	};
	version | grep Version Tag gtt-${client[release]} | prefix silent_
}
{9}

#TICKER           {versionChecker}  {#if {{$busy} != {true} && {${character[inBattle]}} != {true}}{version | grep Version Tag gtt-${client[release]} | prefix silent_}}                                                     {3600}

#VARIABLE         {client[release]}  {beta}
#VARIABLE         {client[lastPingName]}  {}
#VARIABLE         {client[tintinAlterAeonUUID]}  {}
#VARIABLE         {client[tintinAlterAeonVersionNumber]}          {2024.07.22}
