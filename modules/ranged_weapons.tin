#ACTION {^ammo_You get %*${ammo}\.$}
{
    #line {gag};
    #if {{${auto[store]}} == {true}}
    {
        #if {!&{character[ammoContainer]}}
        {
            #variable {character[ammoContainer]} {${character[container]}}
        };
        put $ammo ${character[ammoContainer]} | grep -v You put
    }
}
{8}

#ACTION {^Autoreload container set to '%1'.$}
{
    #regexp {%1} {{.*} {.*}}
    {
        ammocontainer &2
    };
    #else
    {
        ammocontainer %1
    }
}
{9}

#ACTION {^You fire %1 at %2!$}
{
    #send {get $ammo | prefix ammo_}
}
{9}

#ACTION {^You fire %1 at %2, but miss.$}
{
    #nop {Hook name: archery_miss};
    #nop {Hook args: target that was missed.};
    #send {get $ammo | prefix ammo_};
    @do_hook{archery_miss;%2}
}
{9}

#ACTION {^You load %1 with %2.$}
{
    #regexp {%2} {{.*} {.*}}
    {
        #variable {ammo} {&2}
    };
    #else
    {
        #variable {ammo} {%2}
    }
}
{9}

#ACTION {^ammo_%*}
{
    #line {gag}
}
{9}

#ACTION {^{A|An|The} %2 isn't loaded.$}
{
    #if {{${auto[store]}} == {true}}
    {
        #send {get $ammo ${character[ammoContainer]} | grep nospam}
    };
    #regexp {%2} {{.*} {.*}}
    {
        load &2
    };
    #else
    {
        load %2
    };
    fire
}
{9}


#ALIAS {^ammocontainer %S$}
{
    #variable {character[ammoContainer]} {%1};
    #echo {Ammo will now be stored in %1.}
}
{9}


