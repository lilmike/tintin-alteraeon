#ALIAS {^DISPLAY_CLIENT_TIPS$}
{
    #if {!&{auto[clientTips]}}
    {
        #variable {auto[clientTips]} {true}
    };
    #if {!&{client[tips]}}
    {
        LOAD_CLIENT_TIPS
    };
    #if {{${auto[clientTips]}} == {true} && {${character[inBattle]}} != {true} && {$busy} != {true}}
    {
        #if {${client[tipsIndex]} <= ${client[tipsSize]}}
        {
            #echo {Tip:};
            #script {cat ${client[tips][${client[tipsIndex]}]} | while read -r x ; do echo "#showme {$x};" ; done};
            #math {client[tipsIndex]} {${client[tipsIndex]} + 1}
        };
        #else
        {
            #variable {client[tipsIndex]} {1}
        }
    }
}
{9}

#ALIAS {^LOAD_CLIENT_TIPS$}
{
    #variable {client[tips]} {};
    #script {for f in $(ls -1 tips/*.tip | $shuf);do echo "#list {client[tips]} {add} {$f}";done};
    #variable {client[tipsIndex]} {1};
    #list {client[tips]} {size} {client[tipsSize]}
}
{9}

#ALIAS {^{noc|c}tips$}
{
    #if {{%0} == {ctips}}
    {
        #variable {auto[clientTips]} {true};
        #echo {Tintin-alteraeon tips enabled.}
    };
    #else
    {
        #variable {auto[clientTips]} {false};
        #echo {Tintin-alteraeon tips disabled.}
    }
}
{9}




#TICKER {clientTips} {DISPLAY_CLIENT_TIPS} {300}

